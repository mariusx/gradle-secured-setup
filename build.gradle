plugins {
	id 'org.springframework.boot' version '2.6.7'
	id 'io.spring.dependency-management' version '1.0.11.RELEASE'
	id 'java'
	id 'jacoco'
	id 'com.github.spotbugs' version '5.0.6'
	id 'pmd'
	id "org.owasp.dependencycheck" version "7.0.4.1"
}

group = 'biz.marius'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '17'

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-webflux'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'io.projectreactor:reactor-test'
	testImplementation 'org.assertj:assertj-core'

	spotbugsPlugins 'com.h3xstream.findsecbugs:findsecbugs-plugin:1.11.0'
}

jacoco {
	toolVersion = "0.8.7"
	//reportsDir = file("$buildDir/jacoco")
}


test {
	useJUnitPlatform()
	finalizedBy jacocoTestReport
}

jacocoTestReport {
	dependsOn test
}

jacocoTestCoverageVerification {
	violationRules {
		rule {
			element = 'CLASS'
			limit {
				minimum = 1
			}
			excludes = [
				'biz.marius.securedsetup.excluded.ExcludedService',
				'biz.marius.securedsetup.SecuredSetupApplication'
			]
		}
		rule {
			limit {
				counter = 'INSTRUCTION'
				value = 'MISSEDRATIO'
				minimum = 0.3
			}
		}
	}
}

spotbugsMain {
	reports {

		html {
			required = true
			outputLocation = file("$buildDir/reports/spotbugs/main/spotbugs.html")
			stylesheet = 'fancy-hist.xsl'
		}
		xml {
			required = true
		}
	}
}

pmd {
	consoleOutput = true
	toolVersion = "6.44.0"
	rulesMinimumPriority = 5
	ruleSets = [
			"category/java/errorprone.xml",
			"category/java/bestpractices.xml",
			"category/java/security.xml",
			"category/java/design.xml"
	]
}
pmdMain {
	exclude 'biz/marius/**/SecuredSetupApplication.*'
}

dependencyCheck {
	cveValidForHours=1
	// failOnError=true <-- default
	suppressionFiles = ["${rootDir}/dependency-check/suppressions.xml"]
	formats = ['HTML', 'JUNIT']
	failBuildOnCVSS = 4 // medium
}
